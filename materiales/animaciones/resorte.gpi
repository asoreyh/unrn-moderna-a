# spring wave
# a mass m is hanging from a spring of constant k

# general
reset
clear
set samples 5000
set grid
set format x "%.1f"
set format y "%0.2f"
set xlabel "Tiempo t (s)"
set ylabel "Posición y(t)"

# wave motion
k=5.	# spring constant N/m
m=0.13	# mass, in kg
A=0.10  # amplitude, in m
g=9.81  # gravity, in g/cm2

w=sqrt(k/m) 
T=2*pi/w
print "El periodo del péndulo es ", T, " s"

# wave equations
y(t)=A*cos(w*t)				# position
v(t) = -A * w * sin(w*t)	# speed
a(t) = -A * w**2 * cos(w*t)	# acceleration

set xrange [0:3*T]

plot y(x) w l lw 3 lc 1 t "y(t)",\
     v(x) w l lw 3 lc 2 t "v(t)",\
     a(x) w l lw 3 lc 3 t "a(t)"
 
