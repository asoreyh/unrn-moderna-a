reset
set grid
set xlabel "r/a_0"
set key horizontal out
#a0=0.05292

a0=1.

set ylabel "R_n^l(x)"
set xrange[0:16]
set yrange [-0.1:1]
set xtics 1

r10(x)=(2./a0**(3/2))*exp(-x/a0)
r20(x)=(1./(2.*sqrt(2.)*a0**(3/2)))*(2.-x/a0)*exp(-x/(2*a0))
r21(x)=(1./(2.*sqrt(6.)*a0**(3/2)))*(x/a0)*exp(-x/(2*a0))
r30(x)=(2./(81.*sqrt(3.)*a0**(3/2)))*(27.-18*x/a0+2*(x/a0)**2)*exp(-x/(3*a0))
r31(x)=(4./(81.*sqrt(6.)*a0**(3/2)))*(6-(x/a0))*(x/a0)*exp(-x/(3*a0))
r32(x)=(4./(81.*sqrt(30.)*a0**(3/2)))*(x/a0)**2*exp(-x/(3*a0))

plot \
	r10(x) w l lw 3 t "1s", \
	r20(x) w l lw 3 t "2s", \
	r21(x) w l lw 3 t "2p", \
	r30(x) w l lw 3 t "3s", \
	r31(x) w l lw 3 t "3p", \
	r32(x) w l lw 3 t "3d"

pause -1

set ylabel "|R_n^l(x)|^2"
set yrange [0:0.6]

plot \
	(x**2*r10(x)*r10(x)) w l lw 3 t "1s"

pause -1
plot \
	(x**2*r10(x)*r10(x)) w l lw 3 lt 0 t "1s", \
	(x**2*r20(x)*r20(x)) w l lw 3 t "2s", \
	(x**2*r21(x)*r21(x)) w l lw 3 t "2p"
pause -1
plot \
	(x**2*r10(x)*r10(x)) w l lw 3 lt 0 t "1s", \
	(x**2*r30(x)*r30(x)) w l lw 3 t "3s", \
	(x**2*r31(x)*r31(x)) w l lw 3 t "3p", \
	(x**2*r32(x)*r32(x)) w l lw 3 t "3d"

