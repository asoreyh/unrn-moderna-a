# general
reset
set term x11
set grid
set samples 5000
s = 20. # time factor
p = 0.001 # delay between two steps 
n = 100. # number of waves
set key horizontal

# options for wave configuration
# standard wave form: y(x,t) = A*cos(k*x-w*t), 
# so for two waves:
k1=1.
k2=k1*0.80
A1=1.
v=1. ## wave group velocity, so v = w / k
w1=v*k1
w2=v*k2
A2=1.

set xrange [0:pi*n/2.]
set yrange [-(A1+A2):(A1+A2)]

# wave definitions:
y1(A1,x,t)=A1*cos(k1*x-w1*t)
y2(A2,x,t)=A2*cos(k2*x-w2*t)

# loop for wave propagation
# should not modified
do for [t = 0:40*s] {
	plot \
	0 w l lw 2 lc 0 t "",\
	y1(A1,x,t/s) w l lw 2 lc 1 t "Onda 1",\
	y2(A2,x,t/s) w l lw 2 lc 2 t "Onda 2",\
	(y1(A1,x,t/s)+y2(A2,x,t/s)) w l lw 3 lc 7 t "Onda 1 + Onda 2"
 	pause p
}
exit
